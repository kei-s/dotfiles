# .zsh.d/zshenv
# https://github.com/clear-code/zsh.d/blob/master/zshenv

# for sudo
typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({,/usr/local,/usr}/sbin(N-/))

# less
export LESS='--RAW-CONTROL-CHARS'
export LESSOPEN='| /usr/local/bin/src-hilite-lesspipe.sh %s'

# grepの設定
## GNU grepがあったら優先して使う。
if type ggrep > /dev/null 2>&1; then
    grep_command=ggrep
else
    grep_command=grep
fi
## grepのバージョンを検出。
grep_version="$(grep --version | head -n 1 | sed -e 's/^[^0-9.]*\([0-9.]*\)[^0-9.]*$/\1/')"
## デフォルトのオプションの設定
grep_options=""
### ディレクトリー内も再帰的に処理する。
grep_options="-r $grep_options"
### バイナリファイルにはマッチさせない。
grep_options="--binary-files=without-match $grep_options"
### 拡張子が.tmpのファイルは無視する。
grep_options="--exclude=\*.tmp $grep_options"
### 管理用ディレクトリを無視する。
if ${grep_command} --help 2>&1 | ${grep_command} -q -- --exclude-dir; then
    grep_options="--exclude-dir=.svn $grep_options"
    grep_options="--exclude-dir=.git $grep_options"
    grep_options="--exclude-dir=.deps $grep_options"
    grep_options="--exclude-dir=.libs $grep_options"
fi
### 可能なら色を付ける。
if grep --help 2>&1 | grep -q -- --color; then
    grep_options="--color=auto $grep_options"
fi

## デフォルトのオプションを使う。
alias grep="${grep_command} ${grep_options}"

# vim for EDITOR
export EDITOR="vim"
if ! type vim > /dev/null 2>&1; then
  alias vim=vi
fi

# EMAIL
email_files=(~/.zsh.d/email(N-.)
             ~/.email(N-.))
for email_file in ${email_files}; do
  export EMAIL=$(cat "$email_file")
  break
done
