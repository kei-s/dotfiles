# .zsh.d/zshenv
# https://github.com/clear-code/zsh.d/blob/master/zshenv

# for sudo
typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({,/usr/local,/usr}/sbin(N-/))

# less
export LESS='--RAW-CONTROL-CHARS'
export LESSOPEN='| /usr/local/bin/src-hilite-lesspipe.sh %s'

# grep
if type ggrep > /dev/null 2>&1; then
  alias grep=ggrep
fi
grep_version="$(grep --version | head -n 1 | sed -e 's/^[^0-9.]*\([0-9.]*\)[^0-9.]*$/\1/')"
export GREP_OPTIONS
GREP_OPTIONS="--no-messages --binary-files=without-match"
GREP_OPTIONS="--exclude=\*.tmp $GREP_OPTIONS"
case "$grep_version" in
  1.*|2.[0-4].*|2.5.[0-3])
    ;;
  *)
    ### grep 2.5.4以降のみの設定
    ### grep対象としてディレクトリを指定したらディレクトリ内を再帰的にgrepする。
    GREP_OPTIONS="--directories=recurse $GREP_OPTIONS"
    ;;
esac
if grep --help 2>&1 | grep -q -- --exclude-dir; then
  GREP_OPTIONS="--exclude-dir=.svn $GREP_OPTIONS"
  GREP_OPTIONS="--exclude-dir=.git $GREP_OPTIONS"
  GREP_OPTIONS="--exclude-dir=.deps $GREP_OPTIONS"
  GREP_OPTIONS="--exclude-dir=.libs $GREP_OPTIONS"
fi
if grep --help 2>&1 | grep -q -- --color; then
  GREP_OPTIONS="--color=auto $GREP_OPTIONS"
fi

# vim for EDITOR
export EDITOR="vim"
if ! type vim > /dev/null 2>&1; then
  alias vim=vi
fi

# EMAIL
email_files=(~/.zsh.d/email(N-.)
             ~/.email(N-.))
for email_file in ${email_files}; do
  export EMAIL=$(cat "$email_file")
  break
done
